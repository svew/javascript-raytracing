<!--  Demo of lighting using more detailed material and light 
properties.  Same as Lighting2c otherwise.  Only changes are in
the fragment shader, which now gets material and light properties
as 3x3 matrices.  See Lighting3.js.
-->
<!DOCTYPE html>
<html lang="en"><head>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta charset="utf-8">
<title>Light/material properties</title>
</head>

<body> 
	<canvas id="theCanvas" width="600" height="400">
    Please use a browser that supports "canvas"
    </canvas>

<p>
Keyboard controls:
</p><ul style="list-style:none;">
<li>SPACE - pause rotation
</li><li>x - rotate about x axis
</li><li>y - rotate about y axis
</li><li>z - rotate about z axis
</li><li>o - Reset to initial state
</li><li>
</li></ul>

<script id="vertexLightingShader" type="x-shader/x-vertex">
uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;
uniform mat3 normalMatrix;
uniform vec4 lightPosition;

attribute vec4 a_Position;
attribute vec3 a_Normal;

varying vec3 fL;
varying vec3 fN;
varying vec3 fV;
void main() 
{
  // convert position to eye coords
  vec4 positionEye = view * model * a_Position;

  // convert light position to eye coords
  vec4 lightEye = view * lightPosition;

  // vector to light
  fL = (lightEye - positionEye).xyz;

  // transform normal vector into eye coords
  fN = normalMatrix * a_Normal;

  // vector from vertex position toward view point
  fV = normalize(-(positionEye).xyz);

  gl_Position = projection * view * model * a_Position;
}</script>

<script id="fragmentLightingShader" type="x-shader/x-fragment">
precision mediump float;

uniform mat3 materialProperties;
uniform mat3 lightProperties;
uniform float shininess;

varying vec3 fL;
varying vec3 fN;
varying vec3 fV;
void main() 
{
  // normalize after interpolating
  vec3 N = normalize(fN);
  vec3 L = normalize(fL);
  vec3 V = normalize(fV);

  // reflected vector
  vec3 R = reflect(-L, N);

  // multiply each lighting constant with the corresponding material constant,
  // then grab the three columns to get the ambient, diffuse, and specular components
  mat3 products = matrixCompMult(lightProperties, materialProperties);
  vec4 ambientColor = vec4(products[0], 1.0);
  vec4 diffuseColor = vec4(products[1], 1.0);
  vec4 specularColor = vec4(products[2], 1.0);

  // Lambert's law, clamp negative values to zero
  float diffuseFactor = max(0.0, dot(L, N));

  // specular factor from Phong reflection model
  float specularFactor = pow(max(0.0, dot(V, R)), shininess);

  // add the components together
  gl_FragColor = specularColor * specularFactor + diffuseColor * diffuseFactor + ambientColor;
  gl_FragColor.a = 1.0;
}
</script>

<script id="vertexColorShader" type="x-shader/x-vertex">
uniform mat4 transform;
attribute vec4 a_Position;
attribute vec4 a_Color;
varying vec4 color;
void main() 
{
  color = a_Color;
  gl_Position = transform * a_Position;
}
</script>

<script id="fragmentColorShader" type="x-shader/x-fragment">
precision mediump float;
varying vec4 color;
void main() 
{
  gl_FragColor = color;
}
</script>



		<script src="index_files/webgl-utils.js"></script>
		<script src="index_files/webgl-debug.js"></script>
		<script src="index_files/cuon-utils.js"></script>
		<script src="index_files/cuon-matrix.js"></script>
		
		<!-- Use three.js if you have it locally, for easier debugging -->
		<!--script src="../threejs/three.js"></script>--
		
		<!--  otherwise, load the minified version from the www -->
		<script src="index_files/three.js"></script>

		<script src="index_files/Lighting3.js"></script>
		<script>window.onload = main</script>

</body></html>